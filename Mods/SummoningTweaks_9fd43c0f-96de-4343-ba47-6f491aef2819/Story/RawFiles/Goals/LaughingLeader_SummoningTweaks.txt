Version 1
SubGoalCombiner SGC_AND
INITSECTION
// [BEGIN_NO_OSITOOLS]
SysActivateGoal("LLSUMMONINF_LegacyMode");
// [END_NO_OSITOOLS]
KBSECTION
//REGION LEGACY_SCRIPT
// [BEGIN_NO_OSITOOLS]
IF
SavegameLoading(_,_,_,_)
AND
NOT SysIsActive("LLSUMMONINF_LegacyMode")
THEN
SysActivateGoal("LLSUMMONINF_LegacyMode");
// [END_NO_OSITOOLS]

/* [OSITOOLS_ONLY]
IF
SavegameLoading(_,_,_,_)
AND
SysIsActive("LLSUMMONINF_LegacyMode")
THEN
SysCompleteGoal("LLSUMMONINF_LegacyMode");
*/
//END_REGION

//REGION ADD_REMOVE_SKILL
PROC
LLSUMMONINF_AddSkill((CHARACTERGUID)_Player, (STRING)_Skill, (STRING)_AddedSkillFlag)
AND
CharacterHasSkill(_Player, _Skill, 0)
AND
ObjectGetFlag(_Player, _AddedSkillFlag, _Val)
AND
IntegerSubtract(1, _Val, _Notify)
THEN
ObjectSetFlag(_Player, _AddedSkillFlag, 0);
CharacterAddSkill(_Player, _Skill, _Notify);

PROC
LLSUMMONINF_RemoveSkill((CHARACTERGUID)_Player, (STRING)_Skill)
AND
CharacterHasSkill(_Player, _Skill, 1)
THEN
CharacterRemoveSkill(_Player, _Skill);
//END_REGION


//REGION EXTENDER_SUMMONING
/*
IF
CharacterStatusApplied(_Summon, "SUMMONING", _)
AND
CharacterIsSummon(_Summon, 1)
AND
CharacterGetOwner(_Summon, _Player)
AND
CharacterIsPlayer(_Player, 1)
AND
GetTemplate(_Summon, _Template)
AND
IsTagged(_Summon, "TOTEM", _IsTotem)
THEN
DB_LLSUMMONINF_SummonTemplates(_Template, _IsTotem);
SetVarString(_Player, "LLSUMMONINF_LastSummonTemplate", _Template);
LLSUMMONINF_OnSummonAdded(_Player, _Summon);

PROC
LLSUMMONINF_SummonTemplateAtPosition((CHARACTERGUID)_Player, (STRING)_Template, (REAL)_x, (REAL)_y, (REAL)_z, (REAL)_Lifetime)
AND
DB_LLSUMMONINF_SummonTemplates(_Template, _IsTotem)
THEN
NRD_ModCall("LLSummoningTweaks", "SummonTemplateAtPosition", (STRING)_Player, _Template, (STRING)_x, (STRING)_y, (STRING)_z, (STRING)_Lifetime, (STRING)_IsTotem);

PROC
LLSUMMONINF_SummonTemplateAtPosition((CHARACTERGUID)_Player, (STRING)_Template, (REAL)_x, (REAL)_y, (REAL)_z, (REAL)_Lifetime)
AND
DB_LLSUMMONINF_SummonTemplates(_Template, _IsTotem)
AND
CharacterGetLevel(_Player, _Level)
AND
NRD_Summon(_Player, (GUIDSTRING)_Template, _x, _y, _z, _Lifetime, _Level, _IsTotem, 1, _Summon)
AND
RealSum(_y, 2.0, _ty)
THEN
//PlayEffectAtPosition("RS3_FX_UI_Icon_SkipTurn_UsedScroll_01", _x, _ty, _z);
PlayEffect(_Summon, "RS3_FX_UI_Icon_SkipTurn_UsedScroll_01", "Dummy_OverheadFX");
PlayEffectAtPosition("LLSUMMONINF_Skills_InvokeContract_Cast_Summon_01", _x, _y, _z);
PlayEffectAtPosition("RS3_FX_Skills_Totem_Target_Nebula_01", _x, _y, _z);
*/
//END_REGION

//REGION SUMMON_EVENT_PROCS
/* [OSITOOLS_ONLY]
PROC
LLSUMMONINF_OnSummonAdded((CHARACTERGUID)_Player, (CHARACTERGUID)_Summon)
AND
GetUUID(_Player, _UUID)
THEN
NRD_ModCall("LLSummoningTweaks", "UpdateMaxSummons", _UUID);

IF
CharacterStatusApplied(_Target, "LLSUMMONINF_COPY_INFUSIONS", (CHARACTERGUID)_Summoner)
AND
GetUUID(_Summoner, _UUID1)
AND
GetUUID(_Target, _UUID2)
THEN
NRD_ModCall("LLSummoningTweaks", "CopyInfusions", _UUID1, _UUID2);

IF
CharacterStatusApplied(_Target, "LLSUMMONINF_APPLY_INFUSIONS", (CHARACTERGUID)_Summoner)
AND
GetUUID(_Summoner, _UUID1)
AND
GetUUID(_Target, _UUID2)
THEN
NRD_ModCall("LLSummoningTweaks", "ApplyInfusions", _UUID1, _UUID2);
*/

PROC
LLSUMMONINF_OnSummonAdded((CHARACTERGUID)_Player, (CHARACTERGUID)_Summon)
AND
DB_IsPlayer(_Player)
THEN
DB_LLSUMMONINF_Temp_ActiveSummons(_Player, _Summon);

PROC
LLSUMMONINF_OnSummonAdded((CHARACTERGUID)_Player, (CHARACTERGUID)_Summon)
AND
SysCount("DB_LLSUMMONINF_Temp_ActiveSummons", 2, _Count)
AND
_Count > 0
AND
NOT SysIsActive("LLSUMMONINF_TS_SummonTeleporting")
THEN
SysActivateGoal("LLSUMMONINF_TS_SummonTeleporting");

PROC
LLSUMMONINF_OnSummonAdded((CHARACTERGUID)_Player, (CHARACTERGUID)_Summon)
AND
NOT GlobalGetFlag("LLSUMMONINF_DismissSkillAutoAddingDisabled", 1)
THEN
LLSUMMONINF_AddSkill(_Player, "Target_LLSUMMONINF_DismissSummon", "LLSUMMONINF_AddedDismissSkillToSummoner");

PROC
LLSUMMONINF_OnSummonRemoved((CHARACTERGUID)_Player, (CHARACTERGUID)_Summon)
AND
SysCount("DB_LLSUMMONINF_Temp_ActiveSummons", 2, 0)
AND
SysIsActive("LLSUMMONINF_TS_SummonTeleporting")
THEN
SysCompleteGoal("LLSUMMONINF_TS_SummonTeleporting");
//END_REGION

//REGION SUMMONING_APPLIED
IF
CharacterStatusApplied(_Summon, "SUMMONING", _)
AND
IsTagged(_Summon, "TOTEM", 0)
AND
CharacterIsSummon(_Summon, 1)
AND
CharacterGetOwner(_Summon, _Player)
AND
CharacterIsPlayer(_Player, 1)
AND
GetTemplate(_Summon, _Template)
THEN
SetVarString(_Player, "LLSUMMONINF_LastSummonTemplate", _Template);
LLSUMMONINF_OnSummonAdded(_Player, _Summon);
//END_REGION


//REGION AI_CONTROL
QRY
LLSUMMONINF_QRY_CanMakeAutonomous((CHARACTERGUID)_Source, (CHARACTERGUID)_Target)
AND
CharacterGetOwner(_Target, _Source)
THEN
DB_NOOP(1);

QRY
LLSUMMONINF_QRY_CanMakeAutonomous((CHARACTERGUID)_Source, (CHARACTERGUID)_Target)
AND
CharacterGetReservedUserID(_Source, _ID)
AND
CharacterGetReservedUserID(_Target, _ID)
THEN
DB_NOOP(1);

QRY
LLSUMMONINF_QRY_StillHasControllingCharacter((CHARACTERGUID)_Source, (CHARACTERGUID)_Target)
AND
IsTagged(_Source, "LLSUMMONINF_Autonomous", 0)
AND
_Target != _Source
THEN
DB_NOOP(1);

QRY
LLSUMMONINF_QRY_StillHasControllingCharacter((CHARACTERGUID)_Source, (CHARACTERGUID)_Target)
AND
DB_IsPlayer(_Player)
AND
IsTagged(_Player, "LLSUMMONINF_Autonomous", 0)
THEN
DB_NOOP(1);

/*
IF
CharacterStatusApplied(_Char, "LLSUMMONINF_AUTONOMOUS_TOGGLE", (CHARACTERGUID)_Source)
AND
NOT LLSUMMONINF_QRY_StillHasControllingCharacter(_Source, _Char)
THEN
ShowNotification(_Source, "LLSUMMONINF_Notification_LastControllableCharacter");
*/

IF
CharacterStatusApplied(_Char, "LLSUMMONINF_AUTONOMOUS_TOGGLE", (CHARACTERGUID)_Source)
AND
LLSUMMONINF_QRY_CanMakeAutonomous(_Source, _Char)
//AND
//LLSUMMONINF_QRY_StillHasControllingCharacter(_Source, _Char)
AND
IsTagged(_Char, "LLSUMMONINF_Autonomous", _HasTag)
THEN
LLSUMMONINF_ToggleTag(_Char, "LLSUMMONINF_Autonomous", _HasTag);

PROC
LLSUMMONINF_ToggleTag((CHARACTERGUID)_Char, (STRING)_Tag, 0)
THEN
SetTag(_Char, "LLSUMMONINF_Autonomous");

PROC
LLSUMMONINF_ToggleTag((CHARACTERGUID)_Char, (STRING)_Tag, 1)
THEN
ClearTag(_Char, "LLSUMMONINF_Autonomous");

IF
CharacterStatusRemoved(_Char, "LLSUMMONINF_AUTONOMOUS", _)
AND
IsTagged(_Char, "LLSUMMONINF_Autonomous", 1)
AND
CharacterIsDead(_Char, 0)
THEN
ProcObjectTimer(_Char, "LLSUMMONINF_Timers_ReapplyAutonomous", 250);

PROC
ProcObjectTimerFinished((CHARACTERGUID)_Char, "LLSUMMONINF_Timers_ReapplyAutonomous")
AND
IsTagged(_Char, "LLSUMMONINF_Autonomous", 1)
AND
NOT GetStatusTurns(_Char, "LLSUMMONINF_AUTONOMOUS", -1)
THEN
ApplyStatus(_Char, "LLSUMMONINF_AUTONOMOUS", -1.0, 1, _Char);

IF
CharacterStatusRemoved(_Char, "LLSUMMONINF_AI_CONTROL", _)
AND
IsTagged(_Char, "LLSUMMONINF_Autonomous", 1)
AND
CharacterIsDead(_Char, 0)
AND
CharacterIsInCombat(_Char, 1)
THEN
ProcObjectTimer(_Char, "LLSUMMONINF_Timers_ReapplyAiControl", 250);

PROC
ProcObjectTimerFinished((CHARACTERGUID)_Char, "LLSUMMONINF_Timers_ReapplyAiControl")
AND
IsTagged(_Char, "LLSUMMONINF_Autonomous", 1)
AND
NOT GetStatusTurns(_Char, "LLSUMMONINF_AI_CONTROL", -1)
THEN
ApplyStatus(_Char, "LLSUMMONINF_AI_CONTROL", -1.0, 1, _Char);

IF
ObjectEnteredCombat(_Char, _)
AND
IsTagged(_Char, "LLSUMMONINF_Autonomous", 1)
THEN
SetStoryEvent(_Char, "LLSUMMONINF_Autonomous_CombatStarted");

IF
ObjectLeftCombat(_Char, _)
AND
IsTagged(_Char, "LLSUMMONINF_Autonomous", 1)
THEN
SetStoryEvent(_Char, "LLSUMMONINF_Autonomous_CombatEnded");

IF
ObjectWasTagged((CHARACTERGUID)_Char, "LLSUMMONINF_Autonomous")
THEN
SetStoryEvent(_Char, "LLSUMMONINF_AutonomousSet");

IF
ObjectLostTag((CHARACTERGUID)_Char, "LLSUMMONINF_Autonomous")
THEN
SetStoryEvent(_Char, "LLSUMMONINF_AutonomousRemoved");

//Failsafe
IF
GameStarted(_Region, _)
AND
IsGameLevel(_Region, 1)
AND
DB_IsPlayer(_Char)
AND
HasActiveStatus(_Char, "LLSUMMONINF_AI_CONTROL", 1)
AND
NOT DB_CombatCharacters(_Char, _)
THEN
TimerCancel("LLSUMMONINF_Timers_FixAiControl");
TimerLaunch("LLSUMMONINF_Timers_FixAiControl", 250);

IF
TimerFinished("LLSUMMONINF_Timers_FixAiControl")
THEN
IterateParties("LLSUMMONINF_AutomonousAiControlCheck");

IF
StoryEvent((CHARACTERGUID)_Char, "LLSUMMONINF_AutomonousAiControlCheck")
AND
HasActiveStatus(_Char, "LLSUMMONINF_AI_CONTROL", 1)
AND
//CharacterIsInCombat(_Char, 0)
NOT DB_CombatCharacters(_Char, _)
THEN
DebugBreak("[SummoningTweaks] Removing (LLSUMMONINF_AI_CONTROL) status influence from a character not in combat.");
SetStoryEvent(_Char, "LLSUMMONINF_Autonomous_CombatEnded");
//END_REGION

EXITSECTION

ENDEXITSECTION
ParentTargetEdge "__Start"