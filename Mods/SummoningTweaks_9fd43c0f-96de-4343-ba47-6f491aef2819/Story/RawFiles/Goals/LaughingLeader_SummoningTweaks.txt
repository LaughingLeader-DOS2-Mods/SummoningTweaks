Version 1
SubGoalCombiner SGC_AND
INITSECTION

KBSECTION
//REGION SETTINGS
IF
RegionStarted(_)
AND
NOT DB_LLSUMMONINF_SettingsInitialized(1)
THEN
LLSUMMONINF_InitSettings();
DB_LLSUMMONINF_SettingsInitialized(1);

PROC
LLSUMMONINF_InitSettings()
THEN
LLSUMMONINF_InitDemonCodexSettings();
LLSUMMONINF_InitLarianModSettings();

PROC
LLSUMMONINF_Updater_VersionChanged((STRING)_OldVersion, "1.2.0.1")
THEN
LLSUMMONINF_InitSettings();
//END_REGION

//REGION LARIAN_MOD_ENABLED
QRY
LLSUMMONINF_QRY_LarianSummoningModIsEnabled()
AND
SysIsActive("CMP_SummoningImproved_Statuses")
THEN
DB_NOOP(1);
//END_REGION

//REGION ADD_REMOVE_SKILL
PROC
LLSUMMONINF_AddSkill((CHARACTERGUID)_Player, (STRING)_Skill, (STRING)_AddedSkillFlag)
AND
CharacterHasSkill(_Player, _Skill, 0)
AND
ObjectGetFlag(_Player, _AddedSkillFlag, _Val)
AND
IntegerSubtract(1, _Val, _Notify)
THEN
ObjectSetFlag(_Player, _AddedSkillFlag, 0);
CharacterAddSkill(_Player, _Skill, _Notify);

PROC
LLSUMMONINF_RemoveSkill((CHARACTERGUID)_Player, (STRING)_Skill)
AND
CharacterHasSkill(_Player, _Skill, 1)
THEN
CharacterRemoveSkill(_Player, _Skill);
//END_REGION

//REGION SUMMONING_APPLIED
IF
CharacterStatusApplied(_Summon, "SUMMONING", _)
AND
IsTagged(_Summon, "TOTEM", 0)
AND
CharacterIsSummon(_Summon, 1)
AND
CharacterGetOwner(_Summon, _Player)
AND
CharacterIsPlayer(_Player, 1)
AND
GetTemplate(_Summon, _Template)
THEN
SetVarString(_Player, "LLSUMMONINF_LastSummonTemplate", _Template);
LLSUMMONINF_OnSummonAdded(_Player, _Summon);
//END_REGION

//REGION TOTEM_COOLDOWN_RESET
/* [OSITOOLS_ONLY]
IF
CharacterUsedSkill(_Player, "Summon_TotemFromSurface", _, _)
AND
DB_IsPlayer(_Player)
AND
CharacterIsInCombat(_Player, 0)
THEN
NRD_SkillSetCooldown(_Player, "Summon_TotemFromSurface", 0.0);
*/
//END_REGION

//REGION SUMMON_ADDED
PROC
LLSUMMONINF_OnSummonAdded((CHARACTERGUID)_Player, (CHARACTERGUID)_Summon)
AND
NOT HasActiveStatus(_Player, "LLSUMMONINF_MAX_SUMMONS_INC", 1)
THEN
SetStoryEvent(_Player, "LLSUMMONINF_IncreaseMaxSummons");

PROC
LLSUMMONINF_OnSummonAdded((CHARACTERGUID)_Player, (CHARACTERGUID)_Summon)
AND
NOT GlobalGetFlag("LLSUMMONINF_DismissSkillAutoAddingDisabled", 1)
THEN
LLSUMMONINF_AddSkill(_Player, "Target_LLSUMMONINF_DismissSummon", "LLSUMMONINF_AddedDismissSkillToSummoner");

PROC
LLSUMMONINF_OnSummonAdded((CHARACTERGUID)_Player, (CHARACTERGUID)_Summon)
AND
CharacterIsPartyFollower(_Player, 0)
THEN
LLSUMMONINF_TryAddContract(_Player);
//END_REGION

//REGION INITIALIZE_BOOKS
IF
GameStarted(_Level, _)
AND
NOT GlobalGetFlag("LLSUMMONINF_Initialized", 1)
AND
IsGameLevel(_Level, 1)
THEN
TimerCancel("LLSUMMONINF_Timers_InitPartyLeader");
TimerLaunch("LLSUMMONINF_Timers_InitPartyLeader", 250);

IF
TimerFinished("LLSUMMONINF_Timers_InitPartyLeader")
AND
CharacterGetHostCharacter(_Host)
AND
CharacterGameMaster(_Host, _IsGM)
THEN
LLSUMMONINF_InitializePartyLeader(_Host, _IsGM);

PROC
LLSUMMONINF_InitializePartyLeader((CHARACTERGUID)_Host, 0)
THEN
GlobalSetFlag("LLSUMMONINF_Initialized");
SetStoryEvent(_Host, "LLSUMMONINF_MaxSummons_UpdatePlayers");

PROC
LLSUMMONINF_InitializePartyLeader((CHARACTERGUID)_Host, 0)
AND
GlobalGetFlag("LeaderLib_AutoAddModMenuBooksDisabled", 0)
AND
ItemTemplateIsInUserInventory(_Host, "BOOK_LLSUMMONINF_SettingsMenu_bc6991cb-cadc-42a6-a73a-3e9fd1cda02b", 0, 0)
THEN
ItemTemplateAddTo("BOOK_LLSUMMONINF_SettingsMenu_bc6991cb-cadc-42a6-a73a-3e9fd1cda02b", _Host, 1, 0);

//GM Mode
PROC
LLSUMMONINF_InitializePartyLeader((CHARACTERGUID)_Host, 1)
AND
DB_IsPlayer(_Player)
AND
_Player != _Host
AND
NOT GlobalGetFlag("LLSUMMONINF_Initialized", 1)
THEN
GlobalSetFlag("LLSUMMONINF_Initialized");
SetStoryEvent(_Player, "LLSUMMONINF_MaxSummons_UpdatePlayers");

//GM Mode
PROC
LLSUMMONINF_InitializePartyLeader((CHARACTERGUID)_Host, 1)
AND
GlobalGetFlag("LeaderLib_AutoAddModMenuBooksDisabled", 0)
AND
DB_IsPlayer(_Player)
AND
_Player != _Host
AND
ItemTemplateIsInUserInventory(_Player, "BOOK_LLSUMMONINF_SettingsMenu_bc6991cb-cadc-42a6-a73a-3e9fd1cda02b", 0, 0)
THEN
ItemTemplateAddTo("BOOK_LLSUMMONINF_SettingsMenu_bc6991cb-cadc-42a6-a73a-3e9fd1cda02b", _Player, 1, 0);
//END_REGION

//REGION PARTY_EVENTS
IF
CharacterJoinedParty(_Player)
AND
CharacterIsSummon(_Player, 0)
AND
NOT HasActiveStatus(_Player, "LLSUMMONINF_MAX_SUMMONS_INC", 1)
AND
CharacterGetHostCharacter(_Host)
AND
GetVarInteger(_Host, "LLSUMMONINF_MaxSummonsCap", _Max)
AND
_Max > 1
THEN
SetStoryEvent(_Player, "LLSUMMONINF_IncreaseMaxSummons");

IF
CharacterLeftParty(_Player)
AND
CharacterIsSummon(_Player, 0)
AND
CharacterIsControlled(_Player, 0)
AND
HasActiveStatus(_Player, "LLSUMMONINF_MAX_SUMMONS_INC", 1)
THEN
RemoveStatus(_Player, "LLSUMMONINF_MAX_SUMMONS_INC");
//END_REGION

//REGION SETTINGS_MENU
PROC
ProcBlockUseOfItem(_Player, _Book)
AND
GetTemplate(_Book, "BOOK_LLSUMMONINF_SettingsMenu_bc6991cb-cadc-42a6-a73a-3e9fd1cda02b")
AND
NOT QRY_SpeakerIsAvailable(_Player)
AND
CharacterIsInCombat(_Player, 1)
THEN
CharacterStatusText(_Player, "LLSUMMONINF_StatusText_SettingsBookBlocked");
DB_CustomUseItemResponse(_Player, _Book, 0);

IF
CharacterUsedItemTemplate(_Player, "BOOK_LLSUMMONINF_SettingsMenu_bc6991cb-cadc-42a6-a73a-3e9fd1cda02b", _Book)
AND
QRY_SpeakerIsAvailable(_Player)
THEN
DebugBreak("[SummoningTweaks] Opening settings menu.");
SetStoryEvent(_Player, "LLSUMMONINF_MaxSummons_InitDialogVar");
Proc_StartDialog(0, "LLSUMMONINF_SettingsMenu", _Player);

IF
StoryEvent(_Player, "SummoningTweaks_SettingsMenu_OpenRequest")
AND
QRY_SpeakerIsAvailable(_Player)
THEN
SetStoryEvent(_Player, "LLSUMMONINF_MaxSummons_InitDialogVar");
Proc_StartDialog(0, "LLSUMMONINF_SettingsMenu", _Player);

//Set by LLSUMMONINF_Main.gameScript
IF
StoryEvent(_Player, "LLSUMMONINF_MaxSummons_UpdateDialogVar")
AND
GetVarInteger(_Player, "LLSUMMONINF_MaxSummonsCap", _Max)
THEN
DialogSetVariableInt("LLSUMMONINF_SettingsMenu", "LLSUMMONINF_MaxSummonLimit_89adccef-225e-47ab-8f10-5add6644ec3b", _Max);

IF
StoryEvent((CHARACTERGUID)_Player, "LLSUMMONINF_MaxSummons_InitDialogVar")
AND
LLSUMMONINF_QRY_IsHost(_Player)
THEN
UserSetFlag(_Player, "LLSUMMONINF_IsHost");

QRY
LLSUMMONINF_QRY_IsHost((CHARACTERGUID)_Player)
AND
CharacterGetHostCharacter(_Host)
AND
_Player == _Host
THEN
DB_NOOP(1);

QRY
LLSUMMONINF_QRY_IsHost((CHARACTERGUID)_Player)
AND
CharacterGetHostCharacter(_Host)
AND
_Player != _Host
AND
CharacterGetReservedUserID(_Host, _ID)
AND
CharacterGetReservedUserID(_Player, _ID)
THEN
DB_NOOP(1);

IF
DialogStarted("LLSUMMONINF_SettingsMenu", (INTEGER)_Instance)
AND
DialogGetInvolvedPlayer(_Instance, 1, (CHARACTERGUID)_Player)
AND
UserGetFlag(_Player, "LLSUMMONINF_IsHost", 1)
AND
GetVarInteger(_Player, "LLSUMMONINF_MaxSummonsCap", _Max)
THEN
DB_LLSUMMONINF_Temp_StartingMaxSummons(_Player, _Max);

IF
DialogEnded("LLSUMMONINF_SettingsMenu", (INTEGER)_Instance)
AND
DialogGetInvolvedPlayer(_Instance, 1, (CHARACTERGUID)_Player)
AND
UserGetFlag(_Player, "LLSUMMONINF_IsHost", 1)
AND
DB_LLSUMMONINF_Temp_StartingMaxSummons(_Player, _Max)
AND
GetVarInteger(_Player, "LLSUMMONINF_MaxSummonsCap", _NewMax)
AND
_Max != _NewMax
AND
_NewMax >= 0
THEN
SetStoryEvent(_Player, "LLSUMMONINF_MaxSummons_UpdatePlayers");

IF
DialogEnded("LLSUMMONINF_SettingsMenu", (INTEGER)_Instance)
AND
DialogGetInvolvedPlayer(_Instance, 1, (CHARACTERGUID)_Player)
AND
DB_LLSUMMONINF_Temp_StartingMaxSummons(_Player, _Max)
THEN
NOT DB_LLSUMMONINF_Temp_StartingMaxSummons(_Player, _Max);
UserClearFlag(_Player, "LLSUMMONINF_IsHost", 0);
//END_REGION

//REGION DIALOG_COMMANDS
QRY
LLSUMMONINF_QRY_ClearFlag((GUIDSTRING)_Object, (STRING)_Flag)
THEN
ObjectClearFlag(_Object, _Flag);

IF
ObjectFlagSet("LLSUMMONINF_Commands_AddDemonCodex", (CHARACTERGUID)_Player, _)
AND
LLSUMMONINF_QRY_ClearFlag(_Player, "LLSUMMONINF_Commands_AddDemonCodex")
AND
NOT CharacterGetItemTemplateCount(_Player, "BOOK_LLSUMMONINF_DemonCodex_48957893-b552-4642-b864-98f114a6d7fb", 0)
THEN
ShowNotification(_Player, "LLSUMMONINF_Notification_AlreadyHaveDemonCodex");

IF
ObjectFlagSet("LLSUMMONINF_Commands_AddDemonCodex", (CHARACTERGUID)_Player, _)
AND
LLSUMMONINF_QRY_ClearFlag(_Player, "LLSUMMONINF_Commands_AddDemonCodex")
AND
CharacterGetItemTemplateCount(_Player, "BOOK_LLSUMMONINF_DemonCodex_48957893-b552-4642-b864-98f114a6d7fb", 0)
THEN
ItemTemplateAddTo("BOOK_LLSUMMONINF_DemonCodex_48957893-b552-4642-b864-98f114a6d7fb", _Player, 1);
PlayEffect(_Player, "RS3_FX_Skills_Totem_Cast_Target_Totem_Void_Root_02");
PlayEffect(_Player, "RS3_FX_Skills_Totem_Impact_Summon_Void_01");

IF
ObjectFlagSet("LLSUMMONINF_LeaderLib_AddControlSkill", (CHARACTERGUID)_Player, _)
AND
LLSUMMONINF_QRY_ClearFlag(_Player, "LLSUMMONINF_LeaderLib_AddControlSkill")
AND
CharacterHasSkill(_Player, "Target_LeaderLib_ControlSummon", 0)
THEN
CharacterAddSkill(_Player, "Target_LeaderLib_ControlSummon", 1);

IF
ObjectFlagSet("LLSUMMONINF_LeaderLib_RemoveControlSkill", (CHARACTERGUID)_Player, _)
AND
LLSUMMONINF_QRY_ClearFlag(_Player, "LLSUMMONINF_LeaderLib_RemoveControlSkill")
AND
CharacterHasSkill(_Player, "Target_LeaderLib_ControlSummon", 1)
THEN
CharacterRemoveSkill(_Player, "Target_LeaderLib_ControlSummon");

IF
ObjectFlagSet("LLSUMMONINF_Commands_AddDismissSkill", (CHARACTERGUID)_Player, _)
THEN
ObjectClearFlag(_Player, "LLSUMMONINF_Commands_AddDismissSkill");
LLSUMMONINF_AddSkill(_Player, "Target_LLSUMMONINF_DismissSummon", "LLSUMMONINF_AddedDismissSkillToSummoner");

IF
ObjectFlagSet("LLSUMMONINF_Commands_RemoveDismissSkill", (CHARACTERGUID)_Player, _)
THEN
ObjectClearFlag(_Player, "LLSUMMONINF_Commands_RemoveDismissSkill");
LLSUMMONINF_RemoveSkill(_Player, "Target_LLSUMMONINF_DismissSummon");

IF
ObjectFlagSet("LLSUMMONINF_Commands_AddAiToggleSkill", (CHARACTERGUID)_Player, _)
THEN
ObjectClearFlag(_Player, "LLSUMMONINF_Commands_AddAiToggleSkill");
LLSUMMONINF_AddSkill(_Player, "Target_LLSUMMONINF_ToggleAiControl", "LLSUMMONINF_AddedAiControlSkill");

IF
ObjectFlagSet("LLSUMMONINF_Commands_RemoveAiToggleSkill", (CHARACTERGUID)_Player, _)
THEN
ObjectClearFlag(_Player, "LLSUMMONINF_Commands_RemoveAiToggleSkill");
LLSUMMONINF_RemoveSkill(_Player, "Target_LLSUMMONINF_ToggleAiControl");

IF
ObjectFlagSet("LLSUMMONINF_Commands_HostRemoveAllAutonomous", (CHARACTERGUID)_Player, _)
THEN
ObjectClearFlag(_Player, "LLSUMMONINF_Commands_HostRemoveAllAutonomous");
IterateParties("LLSUMMONINF_RemoveAutomonousTag");

IF
StoryEvent(_Char, "LLSUMMONINF_RemoveAutomonousTag")
THEN
ClearTag(_Char, "LLSUMMONINF_Autonomous");

IF
ObjectFlagSet("LLSUMMONINF_Commands_OwnerRemoveAllAutonomous", (CHARACTERGUID)_Player, _)
THEN
ObjectClearFlag(_Player, "LLSUMMONINF_Commands_OwnerRemoveAllAutonomous");
DB_LLSUMMONINF_Temp_RemovingAutonomousTag(_Player);
IterateParty(_Player, "LLSUMMONINF_RemoveAutomonousTag_ByOwner");
ProcObjectTimer(_Player, "LLSUMMONINF_Timers_ClearAutonomousDBIterator", 250);

IF
StoryEvent((CHARACTERGUID)_Char, "LLSUMMONINF_Timers_ClearAutonomousDBIterator")
AND
DB_LLSUMMONINF_Temp_RemovingAutonomousTag(_Player)
AND
LLSUMMONINF_QRY_CanMakeAutonomous(_Player, _Char)
THEN
ClearTag(_Char, "LLSUMMONINF_Autonomous");

PROC
ProcObjectTimerFinished((CHARACTERGUID)_Player, "LLSUMMONINF_Timers_ClearAutonomousDBIterator")
AND
DB_LLSUMMONINF_Temp_RemovingAutonomousTag(_Player)
THEN
NOT DB_LLSUMMONINF_Temp_RemovingAutonomousTag(_Player);
//END_REGION

//REGION COMBAT_ACTIVE_FIX
IF
ObjectTurnStarted((CHARACTERGUID)_Summon)
AND
CharacterIsSummon(_Summon, 1)
AND
CharacterGetOwner(_Summon, _Owner)
AND
HasActiveStatus(_Owner, "LLSUMMONINF_MAX_SUMMONS_INC", 1)
THEN
DB_LLSUMMONINF_Temp_ActiveTurnSummon(_Summon, _Owner);
ProcObjectTimer(_Summon, "LLSUMMONINF_Timers_MakeActiveFix", 500);

IF
CharacterDied(_Summon)
AND
DB_LLSUMMONINF_Temp_ActiveTurnSummon(_Summon, _Owner)
THEN
NOT DB_LLSUMMONINF_Temp_ActiveTurnSummon(_Summon, _Owner);

IF
ObjectTurnEnded((CHARACTERGUID)_Summon)
AND
DB_LLSUMMONINF_Temp_ActiveTurnSummon(_Summon, _Owner)
THEN
NOT DB_LLSUMMONINF_Temp_ActiveTurnSummon(_Summon, _Owner);

IF
ObjectLeftCombat((CHARACTERGUID)_Summon, _)
AND
DB_LLSUMMONINF_Temp_ActiveTurnSummon(_Summon, _Owner)
THEN
NOT DB_LLSUMMONINF_Temp_ActiveTurnSummon(_Summon, _Owner);

PROC
ProcObjectTimerFinished((CHARACTERGUID)_Summon, "LLSUMMONINF_Timers_MakeActiveFix")
AND
CharacterIsControlled(_Summon, 0)
THEN
MakePlayerActive(_Summon);
//END_REGION

//REGION CONTRACT_ADD_BLOCK
PROC
LLSUMMONINF_TryAddContract((CHARACTERGUID)_Player)
AND
NOT GlobalGetFlag("LLSUMMONINF_InvokeContractAutoAddDisabled", 1)
AND
HasActiveStatus(_Player, "LLSUMMONINF_MAX_SUMMONS_INC", 1)
AND
ItemTemplateIsInCharacterInventory(_Player, "Scroll_LLSUMMONINF_Skill_InvokeContract_9c6b4bf8-54df-434d-a4bf-ecad2ce4851c", 0)
AND
ObjectGetFlag(_Player, "LLSUMMONINF_AddedContractToSummoner", _Val)
AND
IntegerSubtract(1, _Val, _Notify) // x = 1 - x :)
THEN
ObjectSetFlag(_Player, "LLSUMMONINF_AddedContractToSummoner");
ItemTemplateAddTo("Scroll_LLSUMMONINF_Skill_InvokeContract_9c6b4bf8-54df-434d-a4bf-ecad2ce4851c", _Player, 1, _Notify);

PROC
ProcBlockUseOfItem(_Player, _Item)
AND
CharacterIsInCombat(_Player, 1)
AND
GetTemplate(_Item, "Scroll_LLSUMMONINF_Skill_InvokeContract_9c6b4bf8-54df-434d-a4bf-ecad2ce4851c")
THEN
ShowNotification(_Player, "LLSUMMONINF_StatusText_InvokeContractBlocked");
DB_CustomUseItemResponse(_Player, _Item, 0);

PROC
ProcBlockUseOfItem(_Player, _Item)
AND
NOT DB_CustomUseItemResponse(_Player, _Item, _)
AND
GetTemplate(_Item, "Scroll_LLSUMMONINF_Skill_InvokeContract_9c6b4bf8-54df-434d-a4bf-ecad2ce4851c")
AND
NOT LLSUMMONINF_QRY_HasLastSummon(_Player)
THEN
ShowNotification(_Player, "LLSUMMONINF_StatusText_InvokeContractInvalid");
DB_CustomUseItemResponse(_Player, _Item, 0);
//END_REGION

//REGION INVOKE_CONTRACT
QRY
LLSUMMONINF_QRY_HasLastSummon((CHARACTERGUID)_Player)
AND
GetVarString(_Player, "LLSUMMONINF_LastSummonTemplate", _Template)
AND
_Template != ""
THEN
DB_NOOP(1);

IF
CharacterUsedSkillOnTarget(_Player, _Target, "Target_LLSUMMONINF_CastLastSummon", _, _)
AND
GetPosition(_Target, _x, _y, _z)
AND
GetVarString(_Player, "LLSUMMONINF_LastSummonTemplate", _Template)
THEN
LLSUMMONINF_SummonTemplateAtPosition_CheckTemplate(_Player, _Template, _x, _y, _z);

IF
CharacterUsedSkillAtPosition(_Player, _x, _y, _z, "Target_LLSUMMONINF_CastLastSummon", _, _)
AND
GetVarString(_Player, "LLSUMMONINF_LastSummonTemplate", _Template)
THEN
LLSUMMONINF_SummonTemplateAtPosition_CheckTemplate(_Player, _Template, _x, _y, _z);

PROC
LLSUMMONINF_SummonTemplateAtPosition_CheckTemplate((CHARACTERGUID)_Player, (STRING)_Template, (REAL)_x, (REAL)_y, (REAL)_z)
AND
_Template != ""
THEN
LLSUMMONINF_SummonTemplateAtPosition_Start(_Player, _Template, _x, _y, _z);

PROC
LLSUMMONINF_SummonTemplateAtPosition_CheckTemplate((CHARACTERGUID)_Player, "", (REAL)_x, (REAL)_y, (REAL)_z)
THEN
ShowNotification(_Player, "LLSUMMONINF_StatusText_InvokeContractInvalid");

PROC
LLSUMMONINF_SummonTemplateAtPosition_Start((CHARACTERGUID)_Player, (STRING)_Template, (REAL)_x, (REAL)_y, (REAL)_z)
AND
NOT LLSUMMONINF_QRY_HasLastSummon(_Player)
THEN
ShowNotification(_Player, "LLSUMMONINF_StatusText_InvokeContractInvalid");

PROC
LLSUMMONINF_SummonTemplateAtPosition_Start((CHARACTERGUID)_Player, (STRING)_Template, (REAL)_x, (REAL)_y, (REAL)_z)
AND
NOT SysIsActive("LaughingLeader_SummoningTweaks_PermanentSummons")
THEN
LLSUMMONINF_SummonTemplateAtPosition(_Player, _Template, _x, _y, _z, 60.0);

PROC
LLSUMMONINF_SummonTemplateAtPosition_Start((CHARACTERGUID)_Player, (STRING)_Template, (REAL)_x, (REAL)_y, (REAL)_z)
AND
SysIsActive("LaughingLeader_SummoningTweaks_PermanentSummons") // Permanent lifetime
AND
NOT DB_LLSUMMONINF_IgnoredTemplates(_Player, _Template)
THEN
LLSUMMONINF_SummonTemplateAtPosition(_Player, _Template, _x, _y, _z, -1.0);

PROC
LLSUMMONINF_SummonTemplateAtPosition_Start((CHARACTERGUID)_Player, (STRING)_Template, (REAL)_x, (REAL)_y, (REAL)_z)
AND
SysIsActive("LaughingLeader_SummoningTweaks_PermanentSummons") // Permanent lifetime
AND
DB_LLSUMMONINF_IgnoredTemplates(_Player, _Template)
THEN
LLSUMMONINF_SummonTemplateAtPosition(_Player, _Template, _x, _y, _z, 60.0);

PROC
LLSUMMONINF_SummonTemplateAtPosition((CHARACTERGUID)_Player, (STRING)_Template, (REAL)_x, (REAL)_y, (REAL)_z, (REAL)_Lifetime)
AND
CharacterGetAbility(_Player, "Summoning", _SummoningAbilityLevel)
AND
CharacterGetLevel(_Player, _Level)
AND
CharacterSummonAtPosition(_Player, _Template, _x, _y, _z, _Lifetime, _Level, _SummoningAbilityLevel, _Summon)
AND
RealSum(_y, 2.0, _ty)
THEN
PlayEffect(_Summon, "RS3_FX_UI_Icon_SkipTurn_UsedScroll_01", "Dummy_OverheadFX");
PlayEffectAtPosition("LLSUMMONINF_Skills_InvokeContract_Cast_Summon_01", _x, _y, _z);
PlayEffectAtPosition("RS3_FX_Skills_Totem_Target_Nebula_01", _x, _y, _z);
//END_REGION

//REGION EXTENDER_SUMMONING
/*
IF
CharacterStatusApplied(_Summon, "SUMMONING", _)
AND
CharacterIsSummon(_Summon, 1)
AND
CharacterGetOwner(_Summon, _Player)
AND
CharacterIsPlayer(_Player, 1)
AND
GetTemplate(_Summon, _Template)
AND
IsTagged(_Summon, "TOTEM", _IsTotem)
THEN
DB_LLSUMMONINF_SummonTemplates(_Template, _IsTotem);
SetVarString(_Player, "LLSUMMONINF_LastSummonTemplate", _Template);
LLSUMMONINF_OnSummonAdded(_Player, _Summon);

PROC
LLSUMMONINF_SummonTemplateAtPosition((CHARACTERGUID)_Player, (STRING)_Template, (REAL)_x, (REAL)_y, (REAL)_z, (REAL)_Lifetime)
AND
DB_LLSUMMONINF_SummonTemplates(_Template, _IsTotem)
THEN
NRD_LuaCall("LLSUMMONINF_Ext_SummonTemplateAtPosition", (STRING)_Player, _Template, (STRING)_x, (STRING)_y, (STRING)_z, (STRING)_Lifetime, (STRING)_IsTotem);

PROC
LLSUMMONINF_SummonTemplateAtPosition((CHARACTERGUID)_Player, (STRING)_Template, (REAL)_x, (REAL)_y, (REAL)_z, (REAL)_Lifetime)
AND
DB_LLSUMMONINF_SummonTemplates(_Template, _IsTotem)
AND
CharacterGetLevel(_Player, _Level)
AND
NRD_Summon(_Player, (GUIDSTRING)_Template, _x, _y, _z, _Lifetime, _Level, _IsTotem, 1, _Summon)
AND
RealSum(_y, 2.0, _ty)
THEN
//PlayEffectAtPosition("RS3_FX_UI_Icon_SkipTurn_UsedScroll_01", _x, _ty, _z);
PlayEffect(_Summon, "RS3_FX_UI_Icon_SkipTurn_UsedScroll_01", "Dummy_OverheadFX");
PlayEffectAtPosition("LLSUMMONINF_Skills_InvokeContract_Cast_Summon_01", _x, _y, _z);
PlayEffectAtPosition("RS3_FX_Skills_Totem_Target_Nebula_01", _x, _y, _z);
*/
//END_REGION

//REGION DEMON_CODEX
PROC
LLSUMMONINF_InitDemonCodexSettings()
THEN
SysClear("DB_LLSUMMONINF_IncarnateInfusion", 3);
DB_LLSUMMONINF_IncarnateInfusion("Target_RangedInfusion", "INF_RANGED", "INF_RANGED");
DB_LLSUMMONINF_IncarnateInfusion("Target_PowerInfusion", "INF_POWER", "INF_POWER");
DB_LLSUMMONINF_IncarnateInfusion("Target_ShadowInfusion", "INF_SHADOW", "INF_SHADOW");
DB_LLSUMMONINF_IncarnateInfusion("Target_WarpInfusion", "INF_WARP", "INF_WARP");
DB_LLSUMMONINF_IncarnateInfusion("Target_FireInfusion", "INF_FIRE", "INF_FIRE_G");
DB_LLSUMMONINF_IncarnateInfusion("Target_IceInfusion", "INF_BLESSED_ICE", "INF_BLESSED_ICE_G");
DB_LLSUMMONINF_IncarnateInfusion("Target_ElectricInfusion", "INF_ELECTRIC", "INF_ELECTRIC_G");
DB_LLSUMMONINF_IncarnateInfusion("Target_PoisonInfusion", "INF_POISON", "INF_POISON_G");
DB_LLSUMMONINF_IncarnateInfusion("Target_NecrofireInfusion", "INF_NECROFIRE", "INF_NECROFIRE_G");
DB_LLSUMMONINF_IncarnateInfusion("Target_WaterInfusion", "INF_NECROFIRE", "INF_WATER_G");
DB_LLSUMMONINF_IncarnateInfusion("Target_AcidInfusion", "INF_ACID", "INF_ACID_G");
DB_LLSUMMONINF_IncarnateInfusion("Target_CursedElectricInfusion", "INF_CURSED_ELECTRIC", "INF_CURSED_ELECTRIC_G");

SysClear("DB_LLSUMMONINF_LarianInfusion", 2);
//For cross-referencing with Larian's DB_CMP_SummoningImproved_Statuses database
DB_LLSUMMONINF_LarianInfusion("Target_RangedInfusion", "RangedInfusion");
DB_LLSUMMONINF_LarianInfusion("Target_PowerInfusion", "PowerInfusion");
DB_LLSUMMONINF_LarianInfusion("Target_ShadowInfusion", "ShadowInfusion");
DB_LLSUMMONINF_LarianInfusion("Target_WarpInfusion", "WarpInfusion");
DB_LLSUMMONINF_LarianInfusion("Target_FireInfusion", "FireInfusion");
DB_LLSUMMONINF_LarianInfusion("Target_IceInfusion", "IceInfusion");
DB_LLSUMMONINF_LarianInfusion("Target_ElectricInfusion", "ElectricInfusion");
DB_LLSUMMONINF_LarianInfusion("Target_PoisonInfusion", "PoisonInfusion");
DB_LLSUMMONINF_LarianInfusion("Target_NecrofireInfusion", "NecrofireInfusion");
DB_LLSUMMONINF_LarianInfusion("Target_WaterInfusion", "WaterInfusion");
DB_LLSUMMONINF_LarianInfusion("Target_AcidInfusion", "AcidInfusion");
DB_LLSUMMONINF_LarianInfusion("Target_CursedElectricInfusion", "CursedElectricInfusion");
DB_LLSUMMONINF_LarianInfusion("Target_OilInfusion", "OilInfusion");
DB_LLSUMMONINF_LarianInfusion("Target_CursedOilInfusion", "CursedOilInfusion");
DB_LLSUMMONINF_LarianInfusion("Target_BloodInfusion", "BloodInfusion");
DB_LLSUMMONINF_LarianInfusion("Target_CursedBloodInfusion", "CursedBloodInfusion");

QRY
LLSUMMONINF_QRY_HasStoredInfusions((CHARACTERGUID)_Summoner)
AND
DB_LLSUMMONINF_DemonCodexInfusions(_Summoner, _Skill)
THEN
DB_NOOP(1);

IF
CharacterStatusApplied(_Incarnate, "LLSUMMONINF_COPY_INFUSIONS", (CHARACTERGUID)_Summoner)
AND
DB_LLSUMMONINF_DemonCodexInfusions(_Summoner, _Skill)
THEN
NOT DB_LLSUMMONINF_DemonCodexInfusions(_Summoner, _Skill);
ObjectSetFlag(_Summoner, "LLSUMMONINF_HadCopiedInfusions");

IF
CharacterStatusApplied(_Incarnate, "LLSUMMONINF_COPY_INFUSIONS", (CHARACTERGUID)_Summoner)
AND
ObjectGetFlag(_Summoner, "LLSUMMONINF_HadCopiedInfusions", 1)
THEN
ShowNotification(_Summoner, "LLSUMMONINF_Notification_InfusionsCleared");
ObjectClearFlag(_Summoner, "LLSUMMONINF_HadCopiedInfusions", 1);

IF
CharacterStatusApplied(_Incarnate, "LLSUMMONINF_COPY_INFUSIONS", (CHARACTERGUID)_Summoner)
AND
NOT LLSUMMONINF_QRY_LarianSummoningModIsEnabled()
AND
IsTagged(_Incarnate, "INCARNATE_S", 1)
AND
DB_LLSUMMONINF_IncarnateInfusion(_Skill, _SmallStatus, _LargeStatus)
AND
HasActiveStatus(_Incarnate, _SmallStatus, 1)
AND
IsSkillActive(_Summoner, _Skill, 1)
THEN
DB_LLSUMMONINF_DemonCodexInfusions(_Summoner, _Skill);

IF
CharacterStatusApplied(_Incarnate, "LLSUMMONINF_COPY_INFUSIONS", (CHARACTERGUID)_Summoner)
AND
NOT LLSUMMONINF_QRY_LarianSummoningModIsEnabled()
AND
IsTagged(_Incarnate, "INCARNATE_G", 1)
AND
DB_LLSUMMONINF_IncarnateInfusion(_Skill, _SmallStatus, _LargeStatus)
AND
HasActiveStatus(_Incarnate, _LargeStatus, 1)
AND
IsSkillActive(_Summoner, _Skill, 1)
THEN
DB_LLSUMMONINF_DemonCodexInfusions(_Summoner, _Skill);

//Larian Mod Version - They changed all the infusion statuses since they can be applied to any Shared summon
IF
CharacterStatusApplied(_Summon, "LLSUMMONINF_COPY_INFUSIONS", (CHARACTERGUID)_Summoner)
AND
LLSUMMONINF_QRY_LarianSummoningModIsEnabled()
AND
GetTemplate(_Summon, _Template)
AND
DB_LLSUMMONINF_LarianInfusion(_Skill, _Infusion)
AND
IsSkillActive(_Summoner, _Skill, 1)
AND
DB_CMP_SummoningImproved_Statuses(_Template, _Infusion, (STRING)_Status)
AND
HasActiveStatus(_Summon, _Status, 1)
THEN
DB_LLSUMMONINF_DemonCodexInfusions(_Summoner, _Skill);

IF
CharacterStatusApplied(_Incarnate, "LLSUMMONINF_COPY_INFUSIONS", (CHARACTERGUID)_Summoner)
AND
LLSUMMONINF_QRY_HasStoredInfusions(_Summoner)
THEN
ShowNotification(_Summoner, "LLSUMMONINF_Notification_InfusionsStored");

IF
CharacterStatusApplied(_Incarnate, "LLSUMMONINF_COPY_INFUSIONS", (CHARACTERGUID)_Summoner)
AND
NOT LLSUMMONINF_QRY_HasStoredInfusions(_Summoner)
AND
ObjectGetFlag(_Summoner, "LLSUMMONINF_HadCopiedInfusions", 0)
THEN
ShowNotification(_Summoner, "LLSUMMONINF_Notification_NoInfusionsStored");

IF
CharacterStatusApplied(_Incarnate, "LLSUMMONINF_APPLY_INFUSIONS", (CHARACTERGUID)_Summoner)
AND
NOT LLSUMMONINF_QRY_LarianSummoningModIsEnabled()
AND
IsTagged(_Incarnate, "INCARNATE_S", 1)
AND
DB_LLSUMMONINF_DemonCodexInfusions(_Summoner, _Skill)
AND
IsSkillActive(_Summoner, _Skill, 1)
AND
DB_LLSUMMONINF_IncarnateInfusion(_Skill, _SmallStatus, _LargeStatus)
AND
HasActiveStatus(_Incarnate, _SmallStatus, 0)
THEN
ApplyStatus(_Incarnate, _SmallStatus, -1.0, 0, _Summoner);

IF
CharacterStatusApplied(_Incarnate, "LLSUMMONINF_APPLY_INFUSIONS", (CHARACTERGUID)_Summoner)
AND
NOT LLSUMMONINF_QRY_LarianSummoningModIsEnabled()
AND
IsTagged(_Incarnate, "INCARNATE_G", 1)
AND
DB_LLSUMMONINF_DemonCodexInfusions(_Summoner, _Skill)
AND
IsSkillActive(_Summoner, _Skill, 1)
AND
DB_LLSUMMONINF_IncarnateInfusion(_Skill, _SmallStatus, _LargeStatus)
AND
HasActiveStatus(_Incarnate, _LargeStatus, 0)
THEN
ApplyStatus(_Incarnate, _LargeStatus, -1.0, 0, _Summoner);

IF
CharacterStatusApplied(_Summon, "LLSUMMONINF_APPLY_INFUSIONS", (CHARACTERGUID)_Summoner)
AND
LLSUMMONINF_QRY_LarianSummoningModIsEnabled()
AND
GetTemplate(_Summon, _Template)
AND
DB_LLSUMMONINF_DemonCodexInfusions(_Summoner, _Skill)
AND
IsSkillActive(_Summoner, _Skill, 1)
AND
DB_LLSUMMONINF_LarianInfusion(_Skill, _Infusion)
AND
DB_CMP_SummoningImproved_Statuses(_Template, _Infusion, (STRING)_Status)
AND
HasActiveStatus(_Summon, _Status, 0)
THEN
ApplyStatus(_Summon, _Status, -1.0, 0, _Summoner);
//END_REGION

//REGION LARIAN_MOD_APPLY_COPY_INFUSIONS_SKILLS
PROC
LLSUMMONINF_InitLarianModSettings()
THEN
//The Larian versions use more generic targeting conditions since any Larian summon can be infused
DB_LLSUMMONINF_LarianSummonModSkillVersions("Target_LLSUMMONINF_CopyInfusions", "Target_LLSUMMONINF_CopyInfusions_LarianModVersion");
DB_LLSUMMONINF_LarianSummonModSkillVersions("Target_LLSUMMONINF_ApplyInfusions", "Target_LLSUMMONINF_ApplyInfusions_LarianModVersion");

IF
SkillAdded(_Char, "Target_LLSUMMONINF_CopyInfusions", _)
AND
LLSUMMONINF_QRY_LarianSummoningModIsEnabled()
THEN
CharacterRemoveSkill(_Char, "Target_LLSUMMONINF_CopyInfusions");
CharacterAddSkill(_Char, "Target_LLSUMMONINF_CopyInfusions_LarianModVersion", 0);

IF
SkillAdded(_Char, "Target_LLSUMMONINF_ApplyInfusions", _)
AND
LLSUMMONINF_QRY_LarianSummoningModIsEnabled()
THEN
CharacterRemoveSkill(_Char, "Target_LLSUMMONINF_ApplyInfusions");
CharacterAddSkill(_Char, "Target_LLSUMMONINF_ApplyInfusions_LarianModVersion", 0);

IF
GameStarted(_Region,_)
AND
IsGameLevel(_Region, 1)
AND
LLSUMMONINF_QRY_LarianSummoningModIsEnabled()
AND
NOT DB_LLSUMMONINF_LarianSummonModActived(_)
THEN
DB_LLSUMMONINF_LarianSummonModActived(1);
LLSUMMONINF_OnLarianSummonModActivated();

PROC
LLSUMMONINF_OnLarianSummonModActivated()
AND
DB_IsPlayer(_Char)
AND
DB_LLSUMMONINF_LarianSummonModSkillVersions(_BaseSkill, _LarianVersionSkill)
AND
CharacterHasSkill(_Char, _BaseSkill, 1)
THEN
CharacterRemoveSkill(_Char, _BaseSkill);
CharacterAddSkill(_Char, _LarianVersionSkill, 0);

IF
GameStarted(_Region,_)
AND
IsGameLevel(_Region, 1)
AND
NOT LLSUMMONINF_QRY_LarianSummoningModIsEnabled()
AND
DB_LLSUMMONINF_LarianSummonModActived(1)
THEN
NOT DB_LLSUMMONINF_LarianSummonModActived(1);
LLSUMMONINF_OnLarianSummonModDeactivated();

PROC
LLSUMMONINF_OnLarianSummonModDeactivated()
AND
DB_IsPlayer(_Char)
AND
DB_LLSUMMONINF_LarianSummonModSkillVersions(_BaseSkill, _LarianVersionSkill)
AND
CharacterHasSkill(_Char, _LarianVersionSkill, 1)
THEN
CharacterRemoveSkill(_Char, _LarianVersionSkill);
CharacterAddSkill(_Char, _BaseSkill, 0);
//END_REGION

//REGION START
IF
GameEventSet("GAMEEVENT_GameStarted")
THEN
DB_LLSUMMONINF_StoryStarted(1);

IF
SavegameLoaded(_,_,_,_)
AND
DB_StoryStarted(_)
THEN
DB_LLSUMMONINF_StoryStarted(1);

IF
DB_LLSUMMONINF_StoryStarted(1)
THEN
DebugBreak("[LaughingLeader_SummoningTweaks] Starting Summoning Tweaks.");
//END_REGION

//REGION VERSIONING
IF
DB_LLSUMMONINF_StoryStarted(1)
THEN
LLSUMMONINF_Updater_SetVersion("1.3.1.0");

IF
GameStarted(_,_)
AND
LLSUMMONINF_Updater_QRY_UpdateNeeded("1.3.1.0")
THEN
LLSUMMONINF_Updater_RemoveOldVersions("1.3.1.0");
LLSUMMONINF_Updater_SetVersion("1.3.1.0");

QRY
LLSUMMONINF_Updater_QRY_UpdateNeeded((STRING)_Version)
AND
NOT DB_Mods_Registered("SummoningTweaks", "LaughingLeader", _Version)
THEN
DB_NOOP(1);

PROC
LLSUMMONINF_Updater_RemoveOldVersions((STRING)_NewVersion)
AND
DB_Mods_Registered("SummoningTweaks", "LaughingLeader", _Version)
AND
_Version != _NewVersion
THEN
NOT DB_Mods_Registered("SummoningTweaks", "LaughingLeader", _Version);
LLSUMMONINF_Updater_VersionChanged(_Version, _NewVersion);

PROC
LLSUMMONINF_Updater_VersionChanged((STRING)_OldVersion, (STRING)_NewVersion)
AND
StringConcatenate("[SummoningTweaks:VersionChanged] Updating registered version [", _OldVersion, _Str)
AND
StringConcatenate(_Str, "] to [", _Str2)
AND
StringConcatenate(_Str2, _NewVersion, _Str3)
AND
StringConcatenate(_Str3, "]", _Str4)
THEN
DebugBreak(_Str4);

PROC
LLSUMMONINF_Updater_SetVersion((STRING)_Version)
AND
GlobalGetFlag("LeaderLib_Initialized", 1)
THEN
DB_LeaderLib_ModApi_RegisterMod("SummoningTweaks", "LaughingLeader", _Version);

PROC
LLSUMMONINF_Updater_SetVersion((STRING)_Version)
AND
NOT GlobalGetFlag("LeaderLib_Initialized", 1)
THEN
DB_Mods_Registered("SummoningTweaks", "LaughingLeader", _Version);
//END_REGION

//REGION LEADERLIB
IF
StoryEvent(_, "LeaderLib_Initialized")
AND
NOT DB_LLSUMMONINF_RegisteredLeaderLibSettings(_)
THEN
DB_LLSUMMONINF_RegisteredLeaderLibSettings(1);

IF
DB_LLSUMMONINF_RegisteredLeaderLibSettings(1)
THEN
DB_LeaderLib_ModApi_RegisterActiveGoal("SummoningTweaks", "LaughingLeader", "LaughingLeader_SummoningTweaks");
DB_LeaderLib_ModApi_RegisterMenu("LaughingLeader.SummoningTweaks", "[Summoning Tweaks] Settings", "LLSUMMONINF_SettingsMenu", "SummoningTweaks", "LaughingLeader");
DB_LeaderLib_ModMenu_RegisteredMenu_OpenEvent("LaughingLeader.SummoningTweaks", "SummoningTweaks_SettingsMenu_OpenRequest");
DB_LeaderLib_Treasure_TreasureItemTemplates("LeaderLib.LeaderTrader.ModBooks", "BOOK_LLSUMMONINF_SettingsMenu_bc6991cb-cadc-42a6-a73a-3e9fd1cda02b", 1, "");
DB_LeaderLib_Treasure_ItemMaxAmount("LeaderLib.LeaderTrader.ModBooks", "BOOK_LLSUMMONINF_SettingsMenu_bc6991cb-cadc-42a6-a73a-3e9fd1cda02b", 4);
DB_LeaderUpdater_RegisteredUUID("SummoningTweaks", "LaughingLeader", 9fd43c0f-96de-4343-ba47-6f491aef2819);

PROC
LLSUMMONINF_Updater_VersionChanged((STRING)_OldVersion, "1.0.2.0")
AND
NOT DB_LeaderLib_ModMenu_RegisteredMenu_OpenEvent("LaughingLeader.SummoningTweaks", _)
THEN
DB_LeaderLib_ModMenu_RegisteredMenu_OpenEvent("LaughingLeader.SummoningTweaks", "SummoningTweaks_SettingsMenu_OpenRequest");
//END_REGION

//REGION LEADERLIB_GLOBAL_SETTINGS
IF
GlobalFlagSet("LLSUMMONINF_SaveGlobalSettings")
THEN
LLSUMMONINF_SaveGlobalSettings();
GlobalClearFlag("LLSUMMONINF_SaveGlobalSettings");

PROC
LLSUMMONINF_SaveGlobalSettings()
AND
GlobalGetFlag("LeaderLib_Initialized", 1)
AND
GlobalGetFlag("LeaderLib_ExtenderIsActive", 1)
AND
CharacterGetHostCharacter(_Host)
THEN
/* Signals LeaderLib to write the global settings file. */
SetStoryEvent(_Host, "LeaderLib_Commands_SaveGlobalSettings");

/* Automatically called by LeaderLib for mod variable registration */
PROC
LeaderLib_GlobalSettings_InitModRegistration()
THEN
DB_LeaderLib_ModApi_GlobalSettings_Register_Flag("SummoningTweaks", "LaughingLeader", "LLSUMMONINF_DismissSkillAutoAddingDisabled");
DB_LeaderLib_ModApi_GlobalSettings_Register_Flag("SummoningTweaks", "LaughingLeader", "LLSUMMONINF_InvokeContractAutoAddDisabled");
DB_LeaderLib_ModApi_GlobalSettings_Register_Integer("SummoningTweaks", "LaughingLeader", "LLSUMMONINF_MaxSummonsCap", 3);

/* Automatically called by LeaderLib when global settings attempts to get an integer variable. 
LeaderLib will save the variable value when DB_LeaderLib_ModApi_GlobalSettings_SaveIntegerVariable is added to.*/
PROC
LeaderLib_GlobalSettings_GetIntegerVariable("SummoningTweaks", "LaughingLeader", "LLSUMMONINF_MaxSummonsCap")
AND
CharacterGetHostCharacter(_Host)
AND
GetVarInteger(_Host, "LLSUMMONINF_MaxSummonsCap", _Max)
THEN
DB_LeaderLib_ModApi_GlobalSettings_SaveIntegerVariable("SummoningTweaks", "LaughingLeader", "LLSUMMONINF_MaxSummonsCap", _Max);

/* Automatically called by LeaderLib when the variable is loaded from global settings*/
PROC
LeaderLib_GlobalSettings_SetIntegerVariable("SummoningTweaks", "LaughingLeader", "LLSUMMONINF_MaxSummonsCap", (INTEGER)_Cap)
AND
IntegerMax(_Cap, 1, _ClampedA)
AND
IntegerMin(_Cap, 999, _ClampedB)
AND
CharacterGetHostCharacter(_Host)
AND
GetVarInteger(_Host, "LLSUMMONINF_MaxSummonsCap", _Current)
AND
_Current != _ClampedB
THEN
SetVarInteger(_Host, "LLSUMMONINF_MaxSummonsCap", _ClampedB);
SetStoryEvent(_Host, "LLSUMMONINF_SetMaxSummonsCapFromCharacter");
//END_REGION

//REGION MIMICRY
IF
StoryEvent(_, "LeaderLib_Initialized")
AND
NOT DB_LLSUMMONINF_RegisteredMimicrySettings(_)
AND
SysIsActive("LLMIME_ZZZ_Updater")
THEN
DB_LLSUMMONINF_RegisteredMimicrySettings(1);
DB_LLMIME_Mimicking_Blacklist_Skills("SummoningTweaks", "Target_LLSUMMONINF_CastLastSummon");
DB_LLMIME_Mimicking_Blacklist_Skills("SummoningTweaks", "Target_LLSUMMONINF_DismissSummon");
DB_LLMIME_Mimicking_Blacklist_Skills("SummoningTweaks", "Target_LLSUMMONINF_CopyInfusions");
DB_LLMIME_Mimicking_Blacklist_Skills("SummoningTweaks", "Target_LLSUMMONINF_ApplyInfusions");
DB_LLMIME_Mimicking_Blacklist_Skills("SummoningTweaks", "Target_LLSUMMONINF_ToggleAiControl");
DB_LLMIME_Mimicking_Blacklist_Skills("SummoningTweaks", "Target_LLSUMMONINF_CopyInfusions_LarianModVersion");
DB_LLMIME_Mimicking_Blacklist_Skills("SummoningTweaks", "Target_LLSUMMONINF_ApplyInfusions_LarianModVersion");
//END_REGION

//REGION AI_CONTROL
QRY
LLSUMMONINF_QRY_CanMakeAutonomous((CHARACTERGUID)_Source, (CHARACTERGUID)_Target)
AND
CharacterGetOwner(_Target, _Source)
THEN
DB_NOOP(1);

QRY
LLSUMMONINF_QRY_CanMakeAutonomous((CHARACTERGUID)_Source, (CHARACTERGUID)_Target)
AND
CharacterGetReservedUserID(_Source, _ID)
AND
CharacterGetReservedUserID(_Target, _ID)
THEN
DB_NOOP(1);

QRY
LLSUMMONINF_QRY_StillHasControllingCharacter((CHARACTERGUID)_Source, (CHARACTERGUID)_Target)
AND
IsTagged(_Source, "LLSUMMONINF_Autonomous", 0)
AND
_Target != _Source
THEN
DB_NOOP(1);

QRY
LLSUMMONINF_QRY_StillHasControllingCharacter((CHARACTERGUID)_Source, (CHARACTERGUID)_Target)
AND
DB_IsPlayer(_Player)
AND
IsTagged(_Player, "LLSUMMONINF_Autonomous", 0)
THEN
DB_NOOP(1);

/*
IF
CharacterStatusApplied(_Char, "LLSUMMONINF_AUTONOMOUS_TOGGLE", (CHARACTERGUID)_Source)
AND
NOT LLSUMMONINF_QRY_StillHasControllingCharacter(_Source, _Char)
THEN
ShowNotification(_Source, "LLSUMMONINF_Notification_LastControllableCharacter");
*/

IF
CharacterStatusApplied(_Char, "LLSUMMONINF_AUTONOMOUS_TOGGLE", (CHARACTERGUID)_Source)
AND
LLSUMMONINF_QRY_CanMakeAutonomous(_Source, _Char)
//AND
//LLSUMMONINF_QRY_StillHasControllingCharacter(_Source, _Char)
AND
IsTagged(_Char, "LLSUMMONINF_Autonomous", _HasTag)
THEN
LLSUMMONINF_ToggleTag(_Char, "LLSUMMONINF_Autonomous", _HasTag);

PROC
LLSUMMONINF_ToggleTag((CHARACTERGUID)_Char, (STRING)_Tag, 0)
THEN
SetTag(_Char, "LLSUMMONINF_Autonomous");

PROC
LLSUMMONINF_ToggleTag((CHARACTERGUID)_Char, (STRING)_Tag, 1)
THEN
ClearTag(_Char, "LLSUMMONINF_Autonomous");

IF
CharacterStatusRemoved(_Char, "LLSUMMONINF_AUTONOMOUS", _)
AND
IsTagged(_Char, "LLSUMMONINF_Autonomous", 1)
AND
CharacterIsDead(_Char, 0)
THEN
ProcObjectTimer(_Char, "LLSUMMONINF_Timers_ReapplyAutonomous", 250);

PROC
ProcObjectTimerFinished((CHARACTERGUID)_Char, "LLSUMMONINF_Timers_ReapplyAutonomous")
AND
IsTagged(_Char, "LLSUMMONINF_Autonomous", 1)
AND
NOT GetStatusTurns(_Char, "LLSUMMONINF_AUTONOMOUS", -1)
THEN
ApplyStatus(_Char, "LLSUMMONINF_AUTONOMOUS", -1.0, 1, _Char);

IF
CharacterStatusRemoved(_Char, "LLSUMMONINF_AI_CONTROL", _)
AND
IsTagged(_Char, "LLSUMMONINF_Autonomous", 1)
AND
CharacterIsDead(_Char, 0)
AND
CharacterIsInCombat(_Char, 1)
THEN
ProcObjectTimer(_Char, "LLSUMMONINF_Timers_ReapplyAiControl", 250);

PROC
ProcObjectTimerFinished((CHARACTERGUID)_Char, "LLSUMMONINF_Timers_ReapplyAiControl")
AND
IsTagged(_Char, "LLSUMMONINF_Autonomous", 1)
AND
NOT GetStatusTurns(_Char, "LLSUMMONINF_AI_CONTROL", -1)
THEN
ApplyStatus(_Char, "LLSUMMONINF_AI_CONTROL", -1.0, 1, _Char);

IF
ObjectEnteredCombat(_Char, _)
AND
IsTagged(_Char, "LLSUMMONINF_Autonomous", 1)
THEN
SetStoryEvent(_Char, "LLSUMMONINF_Autonomous_CombatStarted");

IF
ObjectLeftCombat(_Char, _)
AND
IsTagged(_Char, "LLSUMMONINF_Autonomous", 1)
THEN
SetStoryEvent(_Char, "LLSUMMONINF_Autonomous_CombatEnded");

IF
ObjectWasTagged((CHARACTERGUID)_Char, "LLSUMMONINF_Autonomous")
THEN
SetStoryEvent(_Char, "LLSUMMONINF_AutonomousSet");

IF
ObjectLostTag((CHARACTERGUID)_Char, "LLSUMMONINF_Autonomous")
THEN
SetStoryEvent(_Char, "LLSUMMONINF_AutonomousRemoved");

//Failsafe
IF
GameStarted(_Region, _)
AND
IsGameLevel(_Region, 1)
AND
DB_IsPlayer(_Char)
AND
HasActiveStatus(_Char, "LLSUMMONINF_AI_CONTROL", 1)
AND
NOT DB_CombatCharacters(_Char, _)
THEN
TimerCancel("LLSUMMONINF_Timers_FixAiControl");
TimerLaunch("LLSUMMONINF_Timers_FixAiControl", 250);

IF
TimerFinished("LLSUMMONINF_Timers_FixAiControl")
THEN
IterateParties("LLSUMMONINF_AutomonousAiControlCheck");

IF
StoryEvent((CHARACTERGUID)_Char, "LLSUMMONINF_AutomonousAiControlCheck")
AND
HasActiveStatus(_Char, "LLSUMMONINF_AI_CONTROL", 1)
AND
//CharacterIsInCombat(_Char, 0)
NOT DB_CombatCharacters(_Char, _)
THEN
DebugBreak("[SummoningTweaks] Removing (LLSUMMONINF_AI_CONTROL) status influence from a character not in combat.");
SetStoryEvent(_Char, "LLSUMMONINF_Autonomous_CombatEnded");

//END_REGION


//REGION DEBUG
IF
GameStarted(_,1)
AND
CharacterGetHostCharacter(_Host)
THEN
DB_IsPlayer(_Host);

/* [OSITOOLS_ONLY]
IF
GameStarted(_,1)
THEN
NRD_LuaReset(1);
*/

IF
TextEventSet("llsummon_init")
AND
GetPosition(TRIGGERGUID_StartPoint_001_ab1e4726-a8e5-45a9-9dde-9816629646e3, _x, _y, _z)
AND
CharacterCreateAtPosition(_x, _y, _z, "Dwarves_Hero_Male_Beast_ca2b78c8-85f3-45f1-b8de-9e8363f1aa51", 0, _Host)
THEN
CharacterMakePlayer(_Host, NULL_00000000-0000-0000-0000-000000000000);
DB_IsPlayer(_Host);
//CharacterApplyPreset(_Host, "Conjurer_Act2");
CharacterAddAbility(_Host, "Summoning", 5);
CharacterAddAttribute(_Host, "Memory", 15);
CharacterAddSkill(_Host, "Summon_Incarnate");
CharacterAddSkill(_Host, "Summon_Cat");
CharacterAddSkill(_Host, "Summon_Condor");
CharacterAddSkill(_Host, "Summon_FireSlug");
CharacterAddSkill(_Host, "Summon_SoulWolf");
CharacterOverrideMaxSourcePoints(_Host, 10);
CharacterAddSourcePoints(_Host, 10);
ItemTemplateAddTo("BOOK_LLSUMMONINF_SettingsMenu_bc6991cb-cadc-42a6-a73a-3e9fd1cda02b", _Host, 1, 0);
ItemTemplateAddTo("Scroll_LLSUMMONINF_Skill_InvokeContract_9c6b4bf8-54df-434d-a4bf-ecad2ce4851c", _Host, 1, 0);
MakePlayerActive(_Host);

IF
TextEventSet("llsummon_setup")
AND
CharacterGetHostCharacter(_Host)
THEN
//CharacterApplyPreset(_Host, "Conjurer_Act2");
CharacterMakePlayer(_Host, NULL_00000000-0000-0000-0000-000000000000);
CharacterAddAbility(_Host, "Summoning", 5);
CharacterAddAttribute(_Host, "Memory", 15);
CharacterAddSkill(_Host, "Summon_Incarnate");
CharacterAddSkill(_Host, "Summon_Cat");
CharacterAddSkill(_Host, "Summon_Condor");
CharacterAddSkill(_Host, "Summon_FireSlug");
CharacterAddSkill(_Host, "Summon_SoulWolf");
CharacterOverrideMaxSourcePoints(_Host, 10);
CharacterAddSourcePoints(_Host, 10);
ItemTemplateAddTo("BOOK_LLSUMMONINF_SettingsMenu_bc6991cb-cadc-42a6-a73a-3e9fd1cda02b", _Host, 1, 0);
ItemTemplateAddTo("Scroll_LLSUMMONINF_Skill_InvokeContract_9c6b4bf8-54df-434d-a4bf-ecad2ce4851c", _Host, 1, 0);
MakePlayerActive(_Host);

IF
GameStarted("_TMPL_Sandbox", 1)
AND
NOT GlobalGetFlag("LLSUMMONINF_Initialized", 1)
THEN
TimerCancel("LLSUMMONINF_Timers_InitPartyLeader");
TimerLaunch("LLSUMMONINF_Timers_InitPartyLeader", 500);
TimerCancel("LLSUMMONINF_Timers_FixAiControl");
TimerLaunch("LLSUMMONINF_Timers_FixAiControl", 250);
//END_REGION
EXITSECTION

ENDEXITSECTION
